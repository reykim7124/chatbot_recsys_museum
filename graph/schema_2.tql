define

## attributes ##
category-name sub attribute, value string;
alt-name sub attribute, value string;
###############

## museum ##
phone-number sub attribute, value string;
email sub attribute, value string;
website sub attribute, value string;
facebook sub attribute, value string;
twitter sub attribute, value string;
instagram sub attribute, value string;
description sub attribute, value string;

nearby sub relation,
  relates near-to,
  relates near-by;

museum-categories sub relation,
  relates has-museum,
  relates has-category;

museum sub entity,
  owns place-name @key,
  owns phone-number,
  owns website,
  owns email,
  owns facebook,
  owns twitter,
  owns instagram,
  owns description;

museum sub entity,
  plays location:contains,
  plays museum-categories:has-museum,
  plays addresses:has-museum,
  plays transportations:has-museum,
  plays coordinates:has-museum,
  # plays ticket-categories:has-museum,
  plays ticket-types:has-museum,
  # plays schedule-categories:has-museum,
  plays schedule-days:has-museum,
  plays ticket-price-ranges:has-museum,
  plays nearby:near-by,
  plays nearby:near-to;

museum-category sub entity,
  owns category-name @key,
  plays museum-categories:has-category;

###############

## locations ##
place-name sub attribute, value string;
latitude sub attribute, value double;
longitude sub attribute, value double;
distance sub attribute, value double;
address-text sub attribute, value string;

location sub relation,
  relates falls-within,
  relates contains;

transportations sub relation,
  owns distance,
  relates has-museum,
  relates has-transportation;

addresses sub relation,
  relates has-museum,
  relates has-address;

coordinates sub relation,
  relates has-museum,
  relates has-coordinate;

transportation-types sub relation,
  relates has-transportation,
  relates has-type;

city sub entity,
  owns place-name @key,
  plays location:falls-within;

address sub entity,
  owns address-text @key,
  plays addresses:has-address,
  plays location:contains;

coordinate sub entity,
  owns latitude,
  owns longitude,
  plays location:contains,
  plays coordinates:has-coordinate;

transportation sub entity,
  owns place-name @key,
  plays transportation-types:has-transportation,
  plays transportations:has-transportation;

transportation-type sub entity,
  owns category-name @key,
  plays transportation-types:has-type;

###############

## tickets ##
ticket-name sub attribute, value string;
ticket-price-range-name sub attribute, value string;
price sub attribute, value double;

# ticket-types sub relation,
#   owns price,
#   relates has-ticket-type,
#   relates has-museum,
#   relates has-ticket-category;

ticket-types sub relation,
  owns price,
  owns category-name,
  owns alt-name,
  relates has-ticket-type,
  relates has-museum;

ticket-price-ranges sub relation,
  relates has-museum,
  relates has-ticket-price-range;

# ticket-categories sub relation,
#   owns alt-name,
#   relates has-museum,
#   relates has-ticket-category;

ticket-price-range sub entity,
  owns ticket-price-range-name,
  plays ticket-price-ranges:has-ticket-price-range;

ticket-type sub entity,
  owns ticket-name @key,
  plays ticket-types:has-ticket-type;

# ticket-category sub entity,
#   owns category-name @key,
#   plays ticket-types:has-ticket-category,
#   plays ticket-categories:has-ticket-category;

###############

## schedules ##
open sub attribute, value double;
closed sub attribute, value double;
day sub attribute, value string;

# schedule-categories sub relation,
#   owns alt-name,
#   owns open,
#   owns closed,
#   relates has-museum,
#   relates has-schedule-category;

# schedule-days sub relation,
#   relates has-museum,
#   relates has-schedule-category,
#   relates has-schedule-day;

schedule-days sub relation,
  owns alt-name,
  owns open,
  owns closed,
  owns category-name,
  relates has-museum,
  # relates has-schedule-category,
  relates has-schedule-day;

# schedule-category sub entity,
#   owns category-name @key,
#   plays schedule-categories:has-schedule-category,
#   plays schedule-days:has-schedule-category;

schedule-day sub entity,
  owns day @key,
  plays schedule-days:has-schedule-day;

###############

## rules ##
rule address-within-city:
  when {
    (falls-within: $c, contains: $m) isa location;
    (has-museum: $m, has-address: $a) isa addresses;
  } then {
    (falls-within: $c, contains: $a) isa location;
  };

rule coordinate-within-city:
  when {
    (falls-within: $c, contains: $m) isa location;
    (has-museum: $m, has-coordinate: $co) isa coordinates;
  } then {
    (falls-within: $c, contains: $co) isa location;
  };

rule museum-close-to-each-other:
  when {
    $m1 isa museum;
    $m2 isa museum;
    $t isa transportation;
    (has-museum: $m1, has-transportation: $t) isa transportations, has distance < 10;
    (has-museum: $m2, has-transportation: $t) isa transportations, has distance < 10;
    not { $m1 is $m2; };
  } then {
    (near-by: $m1, near-to: $m2) isa nearby;
  };

rule ticket-price-is-paling-murah:
  when {
    $tpr isa ticket-price-range, has ticket-price-range-name "paling murah";
    $m isa museum;
    $tt isa ticket-type;
    $tts (has-museum: $m, has-ticket-type: $tt) isa ticket-types;
    $tts has price $p;
    $p < 4000;
  } then {
    (has-museum: $m, has-ticket-price-range: $tpr) isa ticket-price-ranges;
  };

rule ticket-price-is-murah:
  when {
    $tpr isa ticket-price-range, has ticket-price-range-name "murah";
    $m isa museum;
    $tt isa ticket-type;
    $tts (has-museum: $m, has-ticket-type: $tt) isa ticket-types;
    $tts has price $p;
    $p >= 4000;
    $p < 15000;
  } then {
    (has-museum: $m, has-ticket-price-range: $tpr) isa ticket-price-ranges;
  };

rule ticket-price-is-mahal:
  when {
    $tpr isa ticket-price-range, has ticket-price-range-name "mahal";
    $m isa museum;
    $tt isa ticket-type;
    $tts (has-museum: $m, has-ticket-type: $tt) isa ticket-types;
    $tts has price $p;
    $p >= 15000;
    $p < 50000;
  } then {
    (has-museum: $m, has-ticket-price-range: $tpr) isa ticket-price-ranges;
  };

rule ticket-price-is-paling-mahal:
  when {
    $tpr isa ticket-price-range, has ticket-price-range-name "paling mahal";
    $m isa museum;
    $tt isa ticket-type;
    $tts (has-museum: $m, has-ticket-type: $tt) isa ticket-types;
    $tts has price $p;
    $p >= 50000;
  } then {
    (has-museum: $m, has-ticket-price-range: $tpr) isa ticket-price-ranges;
  };

rule transportation-has-type-pesawat:
  when {
    $t isa transportation;
    $t has place-name $pn;
    $pn contains "Bandara";
    $ty isa transportation-type, has category-name "pesawat";
  } then {
    (has-transportation: $t, has-type: $ty) isa transportation-types;
  };

rule transportation-has-type-bis:
  when {
    $t isa transportation;
    $t has place-name $pn;
    $pn contains "Terminal";
    $ty isa transportation-type, has category-name "bis";
  } then {
    (has-transportation: $t, has-type: $ty) isa transportation-types;
  };

rule transportation-has-type-kereta:
  when {
    $t isa transportation;
    $t has place-name $pn;
    $pn contains "Stasiun";
    $ty isa transportation-type, has category-name "kereta";
  } then {
    (has-transportation: $t, has-type: $ty) isa transportation-types;
  };
###########